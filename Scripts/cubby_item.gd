extends Area2D

@export var interaction_text: String = "Putting bag away..."
@export var interaction_duration: float = 1.75
@export var interaction_range: float = 100.0
var is_organizing: bool = false
var interaction_elapsed: float = 0.0
var is_mouse_hovering: bool = false
var done: bool = false

func _ready() -> void:
	add_to_group("interactables")
	input_pickable = true
	mouse_entered.connect(func(): is_mouse_hovering = true)
	mouse_exited.connect(func(): is_mouse_hovering = false)

	if has_node("ObjectMarker"):
		$ObjectMarker.visible = false
	if has_node("AnimatedSprite2D"):
		$AnimatedSprite2D.play("Default")

func is_available() -> bool:
	return not done and Global.player_has_backpack and not Global.is_interacting

func _process(delta: float) -> void:
	if is_organizing:
		interaction_elapsed += delta
		var ratio = min(interaction_elapsed / interaction_duration, 1.0)
		Global.interaction_progress.emit(ratio)
		if interaction_elapsed >= interaction_duration:
			_complete_stow()
		return

	# Only show marker and allow interaction when player has a backpack
	if has_node("ObjectMarker"):
		$ObjectMarker.visible = Global.player_has_backpack and not done

	if not done and Global.player_has_backpack and Global.player_node:
		var is_near_player := global_position.distance_to(Global.player_node.global_position) <= interaction_range
		if is_near_player and is_mouse_hovering and (Input.is_action_just_pressed("ui_interact") or Input.is_mouse_button_pressed(MOUSE_BUTTON_LEFT)) and not Global.is_interacting:
			start_stow()

func start_stow() -> void:
	if is_organizing or done:
		return
	is_organizing = true
	interaction_elapsed = 0.0
	Global.is_interacting = true
	Global.interaction_started.emit(interaction_text)

func _complete_stow() -> void:
	is_organizing = false
	interaction_elapsed = 0.0
	done = true
	Global.is_interacting = false
	Global.interaction_finished.emit()

	# Remove backpack from player
	var player = Global.player_node
	if player and player.has_method("remove_backpack"):
		player.remove_backpack()

	# Complete the objective
	Global.request_rule("backpack")
	Global.events_done += 1
	Global.cubby_done += 1
	if Global.cubby_done >= Global.cubby_total:
		Global.objectives_done[1] = true
	Global.task_completed.emit(global_position)
	Global.award_task(self, "bag")

	# Hide marker and switch to filled animation
	if has_node("ObjectMarker"):
		$ObjectMarker.visible = false
	if has_node("AnimatedSprite2D"):
		$AnimatedSprite2D.play("Bag inside")
